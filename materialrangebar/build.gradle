apply plugin: 'com.android.library'
/**
 In order to push the library to Bintray we need to execute the
 following gradle commands in the given sequence.

 ./gradlew assemble      // To generate build folder
 ./gradlew createPom     // To create POM file for maven. if that creates a new file then dont add it to git
 ./gradlew bintrayUpload // To upload library to bintray

 The bintray username and password should be in local.properties (Backup local.properties is on Google Drive)
 */
ext {
    bintrayRepo = 'maven'
    bintrayName = 'MaterialRangebar'

    publishedGroupId = 'com.appyvet'
    libraryName = 'Material Rangebar'
    artifact = 'materialrangebar'

    libraryDescription = 'MaterialRangeBar is a fork from https://github.com/edmodo/range-bar that adds some basic material styling, as well as start and end values, as floats and some other things. It is similar to an enhanced SeekBar widget, though it doesn\'t make use of the SeekBar. It provides for the selection of a range of values as well as for a single value. The selectable range values are discrete values designated by tick marks; the pin (handle) will snap to the nearest tick mark.'

    siteUrl = 'https://github.com/oli107/material-range-bar'
    gitUrl = 'https://github.com/oli107/material-range-bar.git'

    libraryVersion = '1.4.5'

    developerId = 'krazykira'
    developerName = 'Sheraz Ahmed Khilji'
    developerEmail = 'sherazkhilji@gmail.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}
/**
 * This is needed because we want to complie this as android library so that we can test it before publishing
 */
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 1
        versionName libraryVersion
    }
    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
}

dependencies {
    implementation 'com.android.support:support-compat:28.0.0'
}

/**
 Run this code to create pom-default.xml file in build directory
 */
task createPom {
    doLast {
        pom {
            project {
//          All of these values are defined in the ext block
                packaging 'aar'
                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                inceptionYear new Date()
                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }

        }.writeTo("build/poms/pom-default.xml").writeTo("pom.xml")
    }
}
// Place it at the end of the file
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
